# http://kriener.org/articles/2009/06/04/zsh-prompt-magic

setopt prompt_subst
autoload colors
colors

autoload -Uz vcs_info

# set some colors
for COLOR in RED GREEN YELLOW WHITE BLACK CYAN; do
    eval PR_$COLOR='%{$fg[${(L)COLOR}]%}'
    eval PR_BRIGHT_$COLOR='%{$fg_bold[${(L)COLOR}]%}'
done
PR_RESET="%{${reset_color}%}";

# set formats
FMT_BRANCH="${PR_GREEN}%b%u%c${PR_RESET}" # e.g. master?!
FMT_ACTION="(${PR_CYAN}%a${PR_RESET}%)"   # e.g. (rebase-i)
FMT_PATH="%R${PR_YELLOW}/%S"              # e.g. ~/repo/subdir

zstyle ':vcs_info:*:prompt:*' check-for-changes true
zstyle ':vcs_info:*:prompt:*' unstagedstr '?'
zstyle ':vcs_info:*:prompt:*' stagedstr '!'
zstyle ':vcs_info:*:prompt:*' actionformats "${FMT_BRANCH}${FMT_ACTION}//" "${FMT_PATH}"
zstyle ':vcs_info:*:prompt:*' formats       "${FMT_BRANCH}//"              "${FMT_PATH}"
zstyle ':vcs_info:*:prompt:*' nvcsformats   ""                             "%~"

precmd_functions+=prompt_precmd
function prompt_precmd {
    vcs_info 'prompt'
    psvar=()
    psvar[1]=$ZSH_HISTORY_MUTED
}

function lprompt {
    local brackets=$1
    local color1=$2
    local color2=$3

    local bracket_open="${color1}${brackets[1]}${PR_RESET}"
    local bracket_close="${color1}${brackets[2]}"

    local muted="%1(V|${PR_CYAN}-muted- |)"
    local git='$vcs_info_msg_0_'

    local cwd="${color2}%B%1~%b"

    PROMPT="${PR_RESET}${muted}${bracket_open}${git}${cwd}${bracket_close}%# ${PR_RESET}"
}

function rprompt {
    local brackets=$1
    local color1=$2
    local color2=$3

    local bracket_open="${color1}${brackets[1]}${PR_RESET}"
    local bracket_close="${color1}${brackets[2]}${PR_RESET}"
    local colon="${color1}:"
    local at="${color1}@${PR_RESET}"

    local user_host="${color2}%n${at}${color2}%m"
    local vcs_cwd='${${vcs_info_msg_1_%%.}/$HOME/~}'
    local cwd="${color2}%B%20<..<${vcs_cwd}%<<%b"

    RPROMPT="${PR_RESET}${bracket_open}${user_host}${colon}${cwd}${bracket_close}${PR_RESET}"
}

if [ $UID -eq 0 ]; then
  lprompt '<>' $PR_BRIGHT_RED $PR_RED
  rprompt '()' $PR_BRIGHT_RED $PR_RED
else
  lprompt '[]' $PR_WHITE $PR_WHITE
  rprompt '()' $PR_WHITE $PR_WHITE
fi
